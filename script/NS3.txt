#TouhouDanmakufu[Single]
#Title["Boss Nonspell 3"]
#Text[""]
#ScriptVersion[3]

    #include "./../FxnList.txt"
    #include"script/default_system/Default_ShotConst.txt"
    let count = -120; //Counter

    let objBoss;
    let objScene=GetEnemyBossSceneObjectID();

    @Initialize {
    	objBoss = ObjEnemy_Create(OBJ_ENEMY_BOSS);
	ObjEnemy_Regist(objBoss);
	SetDamageRateAtFrame(objBoss,80,50,90);
	ObjMove_SetDestAtFrame(objBoss, GetCenterX, GetCenterY-60, 60); 

	let imgExRumia = GetCurrentScriptDirectory ~ "ExRumia.png";
	ObjPrim_SetTexture(objBoss, imgExRumia);
	ObjSprite2D_SetSourceRect(objBoss, 64, 1, 127, 64);
	ObjSprite2D_SetDestCenter(objBoss);

	NotifyEventAll(EV_USER_STAGE + 2050, ["1", "NS3"]);

	TFinalize;
    }

    @Event{
	alternative(GetEventType())
	case(EV_REQUEST_LIFE){SetScriptResult(5000);}
	case(EV_REQUEST_TIMER){SetScriptResult(120);}
    }

    @MainLoop {
	ObjEnemy_SetIntersectionCircleToShot(objBoss, ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 32); 
        ObjEnemy_SetIntersectionCircleToPlayer(objBoss, ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 24);

	if(count > 0 && count % 120 == 0){
	  ObjMove_SetDestAtFrame(objBoss, rand(GetCenterX+90,GetCenterX-90), rand(GetCenterY-60,GetCenterY-120), 60);
	}

	if(count % 30 == 0 && count > 0) {BulletA;}
	if(count % 60 == 0 && count > 0) {BulletB;}
	
	count++;
	yield;
    }

    task BulletA{
	if(ObjEnemy_GetInfo(objBoss, INFO_LIFE) <= 0){return;}//Default kill to prevent (0,0) spawning
	let ang = rand(0, 360);
	ascent(i in 0..32) {
	    CreateShotA1(ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 3, ang  + 360/32*i, DS_BALL_S_RED, 0);
	}
    }

    task BulletB{
	if(ObjEnemy_GetInfo(objBoss, INFO_LIFE) <= 0){return;}//Default kill to prevent (0,0) spawning
	ascent(i in -2..3) {
	    ascent(j in 0..4) {
	        CreateShotA1(ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 3 + j/4, GetAngleToPlayer(objBoss)  + i*5, DS_BALL_S_BLUE, 0);
	    }
	}
    }

    task TFinalize {
        while(ObjEnemy_GetInfo(objBoss, INFO_LIFE) > 0){yield;}
        loop(11){yield;}
        DeleteShotAll(TYPE_ALL, TYPE_ITEM); //All shots become point items
        Obj_Delete(objBoss);
        DeleteShotAll(TYPE_ALL, TYPE_IMMEDIATE);
        SetAutoDeleteObject(true);
        CloseScript(GetOwnScriptID());
    }